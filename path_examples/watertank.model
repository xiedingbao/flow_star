# Water tank system  (path-oriented reachability analysis)

hybrid reachability
{
 state var h, Fin, x, y, z, T
	
 setting
 {
  fixed steps 0.1
  time 1000
  remainder estimation { h:[-1e-2,1e-2], Fin:[-1e-2,1e-2], x:[-1e-8,1e-8], y:[-1e-4,1e-4], z:[-1e-4,1e-4], T:[-1e-8,1e-8] }
  identity precondition
  gnuplot octagon T,h
  adaptive orders { min { h:3, Fin:3, x:1, y:3, z:3, T:1 } , max { h:5, Fin:5, x:1, y:5, z:5, T:1 }}
  cutoff 1e-12
  precision 53
  output watertank
  max jumps 30
  print on
 }

 modes
 {
  open
  {
   poly ode 1
   {
    h'   = 0.01061*(y + z)*(Fin - 40)
    Fin' = -10*0.01061*(y + z)*(Fin - 40) + 5*(20 - h)
    x'   = 0
    y'   = -y^2 * 0.01061 * (y + z)*(Fin - 40)
    z'   = z^2 * 0.01061 * (y + z)*(Fin - 40)
    T'   = 1
   }

   inv
   {
    h <= 18.01
   }
  }
  
  closing
  {
   poly ode 1
   {
    h'   = 0.01061*(y + z)*(x*Fin - 40)
    Fin' = 0
    x'   = -0.25
    y'   = -y^2 * 0.01061 * (y + z)*(x*Fin - 40)
    z'   = z^2 * 0.01061 * (y + z)*(x*Fin - 40)
    T'   = 1
   }

   inv
   {
    x >= 0
   }
  }

  close
  {
   poly ode 1
   {
    h'   = -0.01061*40*(y + z)
    Fin' = 0
    x'   = 0
    y'   = y^2 * 0.01061*40 * (y + z)
    z'   = -z^2 * 0.01061*40 * (y + z)
    T'   = 1
   }

   inv
   {
    h >= 11.99
   }
  }

  opening
  {
   poly ode 1
   {
    h'   = 0.01061*(y + z)*(x*Fin - 40)
    Fin' = 0
    x'   = 0.25
    y'   = -y^2 * 0.01061 * (y + z)*(x*Fin - 40)
    z'   = z^2 * 0.01061 * (y + z)*(x*Fin - 40)
    T'   = 1
   }

   inv
   {
    x <= 1
   }
  }
  error
  {
   poly ode 1
   {
    h'   = 0
    Fin' = 0
    x'   = 0
    y'   = 0
    z'   = 0
    T'   = 0
   }

   inv
   {
    
   }
  }
 }

 jumps
 {
  open -> closing
  guard
  {
   h in [17.99,18.01]
   y in [0.0555247,0.0555865]
   z in [0.0832639,0.0834029]
  }
  reset { }
  parallelotope aggregation { }

  open -> error
  guard
  {
   y <= 0
  }
  reset { }
  parallelotope aggregation { }

  closing -> close
  guard { x = 0 }
  reset { }
  parallelotope aggregation { }

  close -> opening
  guard
  {
   h in [11.99,12.01]
   y in [0.0832639,0.0834029]
   z in [0.0555247,0.0555865]
  }
  reset { }
  parallelotope aggregation { }

  opening -> open
  guard { x = 1 }
  reset { }
  parallelotope aggregation { }
 }

 init
 {
  open
  {
   h   in [10,10.5]
   Fin in [95,100]
   x   in [1,1]
   y   in [0.095238,0.1]
   z   in [0.05,0.0512821]
   T   in [0,0]
  }
 }
}

#check path{open@closing@close@opening@open@error}
# 	target{error}


